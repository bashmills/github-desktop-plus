# Maintainer: Pol Rivero <aur AT polrivero DOT com>
# Contributor: Caleb Maclennan <caleb AT alerque DOT com>
# Contributor: Ian MacKay <immackay0 AT gmail DOT com>
# Contributor: Mikel Pintado <mikelaitornube2010 AT gmail DOT com>
# Contributor: Igor Petrov
# Contributor: Jiawen Geng

pkgname=github-desktop-plus
pkgver=[[VERSION_WITHOUT_V]]
pkgrel=1
pkgdesc="Fork of GitHub Desktop with extra features and improvements."
arch=('x86_64' 'aarch64')
url="https://github.com/pol-rivero/github-desktop-plus"
license=('MIT')
provides=($pkgname)
conflicts=($pkgname)
depends=(curl
         libcurl-gnutls
         git
         gtk3
         libsecret
         libxss
         nspr
         nss
         org.freedesktop.secrets
         unzip)
optdepends=('github-cli: CLI interface for GitHub'
            'hub: CLI interface for GitHub')
makedepends=(python-setuptools
             'nodejs-lts-[[NODE_CODENAME]]'
             npm
             util-linux
             xorg-server-xvfb
             yarn)
source=("$pkgname::git+https://github.com/pol-rivero/github-desktop-plus.git#tag=v$pkgver"
        'git+https://github.com/github/gemoji.git'
        'git+https://github.com/github/gitignore.git'
        'git+https://github.com/github/choosealicense.com.git'
        'launch-app.sh'
        "$pkgname.desktop")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '[[LAUNCH_SCRIPT_SHA256]]'
            '[[DESKTOP_FILE_SHA256]]')

_deobfuscate() {
    echo "$1" | rev | tr -d '@'
}

prepare() {
    cd "$pkgname"
    git submodule init
    git config submodule."gemoji".url "$srcdir/gemoji"
    git config submodule."app/static/common/gitignore".url "$srcdir/gitignore"
    git config submodule."app/static/common/choosealicense.com".url "$srcdir/choosealicense.com"
    git -c protocol.file.allow=always submodule update
    # https://github.com/shiftkey/desktop/issues/809#issuecomment-1348815685
    sed -e '/compile:prod/s/4096/4096 --openssl-legacy-provider/' -i package.json
}

build() {
    export APP_VERSION="$pkgver"
    cd "$pkgname"
    # https://github.com/nodejs/node/issues/48444
    export UV_USE_IO_URING=0
    xvfb-run yarn install

    # These can be extracted trivially from the app, so there is no point in trying to hide them.
    # Obfuscate them slightly in the PKGBUILD to prevent bots from easily scraping them.    
    export "$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_ID_NAME]]")"="$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_ID]]")"
    export "$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_SECRET_NAME]]")"="$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_SECRET]]")"
    export "$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_ID_BITBUCKET_NAME]]")"="$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_ID_BITBUCKET]]")"
    export "$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_SECRET_BITBUCKET_NAME]]")"="$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_SECRET_BITBUCKET]]")"
    export "$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_ID_GITLAB_NAME]]")"="$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_ID_GITLAB]]")"
    export "$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_SECRET_GITLAB_NAME]]")"="$(_deobfuscate "[[DESKTOP_OAUTH_CLIENT_SECRET_GITLAB]]")"
    xvfb-run yarn build:prod
}

package() {
    INSTALL_DIR="$pkgdir/opt/$pkgname"

    cd "$pkgname"
    install -d "$INSTALL_DIR"
    case "$CARCH" in
        x86_64) suffix="x64" ;;
        aarch64) suffix="arm64" ;;
        *) echo "Unsupported architecture: $CARCH"; exit 1 ;;
    esac
    cp -r --preserve=mode "dist/github-desktop-plus-linux-$suffix/"* "$INSTALL_DIR/"

    cd "$INSTALL_DIR/resources/app/static/logos"
    install -Dm0644 "1024x1024.png" "$pkgdir/usr/share/icons/hicolor/1024x1024/apps/$pkgname.png"
    install -Dm0644 "512x512.png" "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"
    install -Dm0644 "256x256.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/$pkgname.png"

    install -Dm755 "$srcdir/launch-app.sh" "$pkgdir/usr/bin/$pkgname"

    chmod +x "$INSTALL_DIR/resources/app/static/github"
    ln -s "/opt/$pkgname/resources/app/static/github" "$pkgdir/usr/bin/github-desktop-plus-cli"

    install -Dm0644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
